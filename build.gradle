plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.3.0'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.obimp'
version = '0.1.11'

repositories {
    mavenCentral()
}

kotlin {
    jvmToolchain(25)
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = 'ossrh-staging-api'
            url = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
            credentials {
                username = project.findProperty('ossrh.user') ?: System.getenv('OSSRH_USERNAME')
                password = project.findProperty('ossrh.key') ?: System.getenv('OSSRH_PASSWORD')
            }
        }
        maven {
            name = 'GithubPackages'
            url = 'https://maven.pkg.github.com/obimp/obimp4j-core'
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('GPR_USERNAME')
                password = project.findProperty('gpr.key') ?: System.getenv('GPR_TOKEN')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'obimp4j-core'
            from components.java
            pom {
                name = 'OBIMP4J'
                description = 'Java OBIMP (Open Binary Instant Messaging Protocol) implementation, core library.'
                url = 'https://github.com/obimp/obimp4j-core'
                licenses {
                    license {
                        name = 'The GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'libalex'
                        name = 'Alexander Krysin'
                        email = 'alessandro56903@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/obimp/obimp4j-core.git'
                    developerConnection = 'scm:git:ssh://github.com/obimp/obimp4j-core.git'
                    url = 'https://github.com/obimp/obimp4j-core'
                }
            }
        }
    }
}

signing {
    if (System.properties['os.name'].toLowerCase(Locale.ROOT).contains('windows')) {
        useGpgCmd()
    } else {
        def signingKey = findProperty('signingKey')
        def signingPassword = findProperty('signingPassword')
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}